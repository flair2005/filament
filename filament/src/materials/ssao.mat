material {
    name : ssao,
    parameters : [
        {
           type : sampler2d,
           name : depth
        },
        {
            type : float,
            name : radius
        },
        {
            type : float,
            name : bias
        },
        {
            type : float,
            name : power
        }
    ],
    variables : [
         vertex
    ],
    vertexDomain : device,
    depthWrite : false,
    depthCulling : false,
    shadingModel : unlit,
    variantFilter : [ skinning ],
    culling: none
}

vertex {
    void materialVertex(inout MaterialVertexInputs material) {
        // far-plane in view space
        vec4 position = getPosition(); // clip-space
        material.vertex.xy = (position.xy * 0.5 + 0.5);
        material.vertex.zw = position.xy;
    }
}

fragment {
    const uint kSphereSampleCount = 16u;
    const vec3 kSphereSamples[16] = vec3[](
        vec3(-1.60954e-06,3.93118e-07,1.51895e-06), vec3(-0.0950889,0.00458908,-0.0312535),
        vec3(0.0151799,-0.0255864,0.00376453), vec3(0.0734262,0.0218022,0.0027781),
        vec3(0.0945874,0.0432184,0.0891475), vec3(-0.00950861,0.0513686,0.019673),
        vec3(0.139973,-0.101685,0.10857), vec3(-0.103804,0.219853,-0.0430164),
        vec3(0.00484053,-0.0339878,0.0941868), vec3(0.0280114,0.0584662,-0.25711),
        vec3(-0.0510306,0.074993,0.259843), vec3(0.118822,-0.186537,-0.134192),
        vec3(0.0639494,-0.0948936,-0.072683), vec3(0.108176,0.327108,-0.254058),
        vec3(-0.0471796,0.21918,0.263895), vec3(-0.407709,0.240834,-0.200352)
    );

    const uint kNoiseSampleCount = 16u;
    const vec3 kNoiseSamples[kNoiseSampleCount] = vec3[](
        vec3(-0.0782473,-0.749924,-0.65688), vec3(-0.572319,-0.102379,-0.813615), vec3(0.0486527,-0.380791,0.92338), vec3(0.281202,-0.656664,-0.699799),
        vec3(0.711911,-0.235841,-0.661485), vec3(-0.445893,0.611063,0.65405), vec3(-0.703598,0.674837,0.222587), vec3(0.768236,0.507457,0.390257),
        vec3(-0.670286,-0.470387,0.57398), vec3(0.199235,0.849336,-0.488808), vec3(-0.768068,-0.583633,-0.26352), vec3(-0.89733,0.328853,0.294372),
        vec3(-0.57093,-0.531056,-0.626114), vec3(0.699014,0.0632826,-0.712303), vec3(0.207495,0.976129,-0.0641723), vec3(-0.0609008,-0.869738,-0.489742)
    );

    const uint kSpiralSampleCount = 7;
    const vec3 kSipralSamples[kSpiralSampleCount] = vec3[](
        vec3(0.966326,0.257322,0.00591716), vec3(-0.69718,0.716896,0.0532544), vec3(0.975223,0.221226,0.147929),
        vec3(0.982771,0.184826,0.289941), vec3(-0.613055,0.79004,0.47929), vec3(0.997237,0.0742889,0.715976),
        vec3(1,-1.16027e-06,1)
    );

    float random(const vec2 st) {
        return fract(sin(dot(st.xy, vec2(12.9898,78.233)))* 43758.5453123);
    }

    vec3 getNoise(const vec2 uv) {
        uint ix = uint(gl_FragCoord.x) & 3u;
        uint iy = uint(gl_FragCoord.y) & 3u;
        vec3 noise = kNoiseSamples[ix + iy * 4];
        //vec3 noise = normalize(vec3(random(uv), random(uv*2.0), random(uv*4.0)));
        return noise;
    }

    vec3 computeViewSpaceNormalNotNormalized(const vec3 position) {
        vec3 dpdx = dFdx(position);
        vec3 dpdy = dFdy(position);
        return cross(dpdx, dpdy);
    }

    float linearizeDepth(float depth) {
        mat4 projection = getClipFromViewMatrix();
        float z = depth * 2.0 - 1.0; // depth in clip space
        return -projection[3].z / (z + projection[2].z);
    }

    float sampleDepthLinear(const vec2 uv) {
        return linearizeDepth(texture(materialParams_depth, uv, 0.0).r);
    }

    vec3 computeViewSpacePositionFromDepth(in vec2 p, float linearDepth) {
        mat4 projection = getClipFromViewMatrix();
        p.x = p.x * (-1.0 / projection[0].x);   // could be in a ubo
        p.y = p.y * (-1.0 / projection[1].y);   // could be in a ubo
        return vec3(p * linearDepth, linearDepth);
    }

    float computeAmbientOcclusion(const vec3 origin, const vec3 normal, const vec3 noise, const vec3 sphereSample) {
        mat4 projection = getClipFromViewMatrix();
        float radius = materialParams.radius;
        float bias = materialParams.bias;

        vec3 r = sphereSample * radius;
        r = reflect(r, noise);
        r = sign(dot(r, normal)) * r;
        vec3 samplePos = origin + r;

        vec4 samplePosScreen = projection * vec4(samplePos, 1.0);
        samplePosScreen.xy = samplePosScreen.xy * (0.5 / samplePosScreen.w) + 0.5;

        float occlusionDepth = sampleDepthLinear(samplePosScreen.xy);

        // smoothstep() optimized for range 0 to 1
        float t = saturate(radius / abs(origin.z - occlusionDepth));
        float rangeCheck = t * t * (3.0 - 2.0 * t);

        float d = samplePos.z - occlusionDepth; // distance from depth to sample
        return (d >= -bias ? 0.0 : rangeCheck);
    }

    vec3 tapLocation(uint i, const mat2 M) {
        //float radius = float(i + mod(M[0][0], 1.0) + 0.5) * (1.0 / (kSpiralSampleCount - 0.5));
        //float angle = radius * radius * (7.0 * 6.28) + M[0][0];
        //return vec3(cos(angle), sin(angle), radius * radius);
        return vec3(M * kSipralSamples[i].xy, kSipralSamples[i].z);
    }

    float computeAmbientOcclusion2(uint i, float ssDiskRadius, const ivec2 ssOrigin, const vec3 origin, const vec3 normal, const mat2 M) {
        float radius = materialParams.radius;
        float invRadiusSquared = 1.0 / (radius * radius);
        float bias = materialParams.bias;

        vec3 tl = tapLocation(i, M);
        float ssRadius = max(0.75, tl.z * ssDiskRadius);
        ivec2 ssSamplePos = ssOrigin + ivec2(ssRadius * tl.xy);

        ivec2 textureSamplePos = clamp(ssSamplePos, ivec2(0), textureSize(materialParams_depth, 0) - ivec2(1));
        float occlusionDepth = linearizeDepth(texelFetch(materialParams_depth, textureSamplePos, 0).r);

        vec2 uvSamplePos = (vec2(ssSamplePos) + vec2(0.5)) * frameUniforms.resolution.zw;
        vec3 p = computeViewSpacePositionFromDepth(uvSamplePos * 2.0 - 1.0, occlusionDepth);

        // now we have the sample, compute AO
        vec3 v = p - origin;        // sample vector
        float vv = dot(v, v);       // squared distance
        float vn = dot(v, normal);  // distance * cos(v, normal)
        const float uu = 0.015;
        float falloff = saturate(1.0 - vv * invRadiusSquared);
        float occlusion = falloff * max(0.0, vn - bias) / (vv + uu);
        return occlusion;
    }

    void material(inout MaterialInputs material) {
        prepareMaterial(material);

        float radius = materialParams.radius;

        // Pixels-per-meter at z=-1
        // -height / (2.0 * tan(verticalFieldOfView * 0.5))
        const float projScale = 500;

        vec2 uv = variable_vertex.xy; // interpolated to pixel center

        vec3 noise = getNoise(uv);
        mat2 M;
        M[0] = noise.xy;
        M[1] = vec2(-noise.y, noise.x);

        float depth = sampleDepthLinear(uv);
        vec3 origin = computeViewSpacePositionFromDepth(variable_vertex.zw, depth);
        ivec2 ssOrigin = ivec2(uv * frameUniforms.resolution.xy);
        vec3 normal = computeViewSpaceNormalNotNormalized(origin);

        // attempt to reject "bad" normals that were reconstructed at an edge that cause
        // false occlusion (black spots)
        // Normal's length should be small before they're normalized, unless they're bad ones.
        if (dot(normal, normal) >= (sq(origin.z * origin.z * 0.000061))) {
            // For now we assume no occlusion, which is wrong in some case, but overall seem to
            // look better.  Maybe those could be handled in the blur pass instead?
            material.baseColor.r = 1.0;
            return;
        }

        // Choose the screen-space sample radius
        // proportional to the projected area of the sphere
        float ssDiskRadius = -projScale * radius / origin.z;
        if (ssDiskRadius <= 1.0) {
            // There is no way to compute AO at this radius
            material.baseColor.r = 1.0;
            return;
        }

        normal = normalize(normal);
        float occlusion = 0.0;
        float ao;

        if (uv.x < 0.5) {
            for (uint i = 0u; i < kSphereSampleCount; i++) {
                occlusion += computeAmbientOcclusion(origin, normal, noise, kSphereSamples[i]);
            }
            ao = 1.0 - occlusion / float(kSphereSampleCount);
        } else {
            for (uint i = 0u; i < kSpiralSampleCount; i++) {
                occlusion += computeAmbientOcclusion2(i, ssDiskRadius, ssOrigin, origin, normal, M);
            }
            ao = 1.0 - occlusion / float(kSpiralSampleCount);
        }

        // simulate user-controled ao^n with n[1, 2]
        ao = mix(ao, ao * ao, materialParams.power);
        material.baseColor.r = ao;
    }
}
